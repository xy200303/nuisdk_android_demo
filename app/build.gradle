apply plugin: 'com.android.application'

static def getGitRevision() {
    return "git rev-parse --short HEAD".execute().text.trim()
}

android {
    namespace 'mit.alibaba.nuidemo'
    compileSdkVersion 36
    buildFeatures { buildConfig = true }
    defaultConfig {
        applicationId "mit.alibaba.nuidemo"
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86_64"
        }
        // 给gradle.properties中的LATEST_COMMIT_ID赋值
        GIT_COMMIT_ID = getGitRevision()
        DEMO_VERSION = "DEMOv2.7.0-SDKv2.7.1"
        // Java代码调用
        buildConfigField "String", "gitVersionId", "\"${DEMO_VERSION}  ${GIT_COMMIT_ID}\""
    }
    signingConfigs {
        releaseConfig {
            if (file('../keystore.props.file').exists()) {
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(file('../keystore.props.file')))
                storeFile file(keyProps['RELEASE_STORE_FILE'])
                storePassword keyProps['RELEASE_STORE_PASSWORD']
                keyAlias keyProps['RELEASE_KEY_ALIAS']
                keyPassword keyProps['RELEASE_KEY_PASSWORD']
            }
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.releaseConfig
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    lint {
        abortOnError false
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support'
    })
    implementation 'com.android.support:appcompat-v7:28.0.1'
    implementation 'com.squareup.okhttp3:okhttp-sse:5.3.2'
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    implementation files('libs/fastjson-1.1.46.android.jar')
    testImplementation 'junit:junit:4.13.2'

    // find aar package from local dir.
    implementation files ('libs/nuisdk-release.aar')

    // find aar package from Maven
//    implementation 'com.alibaba.idst:nui-01B:2.6.6@aar'
//    implementation 'com.alibaba.idst:nui-01B:2.6.6:debug@aar'
}
